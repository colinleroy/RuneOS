# Maintainer: Lukas Fleischer 
# Contributor: Paul Mattal
# For RuneAudio: janui

pkgname=lirc-rune
_pkgname=lirc
pkgver=0.9.4c
pkgrel=1
pkgdesc="Linux Infrared Remote Control utilities for RuneAudio"
arch=('x86_64' 'armv6h' 'armv7h')
url="http://www.lirc.org/"
license=('GPL')
depends=('alsa-lib' 'libx11' 'libftdi' 'libusb-compat')
makedepends=('help2man' 'alsa-lib' 'libx11' 'libxslt' 'python' 'python-setuptools')
optdepends=('python: for lirc-setup, irdb-get and pronto2lirc')
provides=('lirc-utils')
conflicts=('lirc-utils' 'lirc')
replaces=('lirc-utils' 'lirc')
backup=('etc/lirc/lirc_options.conf' 'etc/lirc/lircd.conf' 'etc/lirc/lircmd.conf')
source=("https://prdownloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"
        lirc.logrotate
        lirc.tmpfiles)
sha1sums=('e6a04fa7447b2c3bc5bac7658fa2cc377ae133ed'
          '9b4646fea360b59c9a7e85e75415a55889cf84c3'
          '494ea6b898729a8a1ac5f83b18dec2ad4eded5d0')

prepare() {
  cd "${srcdir}/lirc-${pkgver}"
  autoreconf -fi
  automake -ac
}

build() {
  cd "${srcdir}/lirc-${pkgver}"

  HAVE_UINPUT=1 ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --enable-devinput
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/lirc-${pkgver}"

  make DESTDIR="${pkgdir}" -j1 install

  install -Dm644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
  install -Dm644 "${srcdir}"/lirc.logrotate "${pkgdir}"/etc/logrotate.d/lirc

  rmdir "${pkgdir}"/var/{run/lirc/,run/}
}
